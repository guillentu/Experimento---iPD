\=============================================================================\
\======================== Magazine para el comedero ==========================\
\ El entrenamiento de magazine está destinado a entrenar al animal para apren \
\ der a comer del comedero. En la caja existen dos palancas en el frente y un \
\ comedero atrás, además existe una palanca externa para realizar el magazine \
\ y posteriomente el shaping.                                                 \
\                                                                             \
\                                                               (Delmas,2015) \
\=============================================================================\
\======================== Magazine para el comedero ==========================\
\ Protocolo                                                                   \
\ Nº de trials = 30
\ Tiempo p\ palanqueo = 1min (def=10s)
\ Tiempo p\ comer     = 10s (def=5)
\ ITI                 = 10s (def=5) Luz apagada 

\=Variables===================================================================\
\ A=Trials
\ B=Respuestas
\ pararComp=Detiene el comparador de tiempo p/ palanqueo
\ Variables a guardar en disco en cada 'flush'.
DISKVARS=A,B

dim H=2 \Reloj
dim J=2
dim K=2
\=============================================================================\

\=SALIDAS=====================================================================\
\ ^constantes
^luzCaja=1 \\puertos
^refuerzo=2
^palancaDer=3
^palancaIzq=4
^palancaShaping=15
^luzPalancaDer=5
^luzPalancaIzq=6
^luzComedero=7

\ ^paramatros de los  trials                                                  \
^nTrials=30
\tPalanca=60  \definidos en comienzo de trial
\tComer=10
\ITI=10
\=============================================================================\
\ Z-PULSE en uso
\ Z1 = empieza el trial 
\ Z2 = Inicia comparacion, espera hasta q se cumpla el tiempo p/ palanquear
\      Luego se va a ITI
\ Z3 = Fin de trial (continua ITI sin luz)
\ Z4 = Inicio de ITI
\ Z5 = Control de luces de palanca
\=============================================================================\

S.S.1, \Control de la cantidad de experimentos y seteo de tiempos
  S1,
    #start:Z1;set A=0;set B=0;set pararComp=0;set ITI=10"; tComer=10" --->S2
  S2,
    #Z1:add A;show 1, Trial, A; if (A=^nTrials) [@fin,@sigTrial]
                                                  @fin:--->S5
                                                  @sigTrial:flush --->S3
  S3, \Enciende las luces estimulos
    0.01":ON^luzCaja;OFF^luzComedero;                 
    time H(0) H(1) H(2); 
    set I=H(0)*3600+H(1)*60+H(2)+^tPalanca;Z2 ---> S4 
                              \ + tiempo de trial
  S4, \Apaga las luces estimulos
    #Z3:ON^luzCaja;OFF^luzComedero;
    show 2, finPalanqueo;Z4 ---> S2 \ espera fin de ITI  
  S5, 
    0.01":OFF^luzCaja --->stopabortflush \Fin del experimentos

  
S.S.2,  \Reloj : contador de tiempo infinito
  S1,
    #START:--->S2
  S2,
    0.01":TIME  J(0), J(1), J(2); SET K=J(0)*3600 + J(1)*60 + J(2) --->S3
  S3,
    0.01":--->S2


S.S.3,  \Control de tiempo p/ palanquear, luego se va a ITI
  S1,
    #Z2: --->S2
  S2,         Reloj > tiempo p/ palanquear
    0.01": if (K > I) [@finTrial,@cont]
                        @finTrial:Z3 ---> S1 \ dispara apagar todo y luego ITI
                        @cont: --->S3
  S3,
    0.01": if (pararComp=1) [@parar,@cont]
                              @parar: ---> S1  \Si para es xq se palanqueo
                              @cont: ---> S2

S.S.4,  \ITI
  S1,
    #Z4: ---> S2
  S2,
    ITI#T:Z1 ---> S1  \ITI es el tiempo de ITI seteado en S.S.1


S.S.5, \Palanca Externa p/ Shaping Habilita Refuerzo
  S1,
    #^palancaShaping:set pararComp=1;Z5 ---> S2  \Z5 Ahora no hace nada
  S2,
    0.1":ON^luzComedero --->S3
  S3,
    0.2":ON^refuerzo ---> S4  \Ver si es necesario sonido
  S4,
    0.01":OFF^refuerzo ---> S5
  S5,
    0.01":show 3, Refuerzo entregado; --->S6
  S6,
    tComer#T:Z3 --->S1  \Se va a apagar todo y al ITI
		
		
S.S.6, \Control de luces de palancas
  S1, 
    #Z5: ---> S2
  S2,
    0.01: ---> S1















